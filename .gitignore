/*
     CÓDIGO:    Q0702
     AUTOR:     BrincandoComIdeias
     SKETCH:    Seguidor de Linha
*/

// DEFINIÇÕES DE PINOS
#define pinSensorDir 4
#define pinSensorEsq 3

#define dirFrente 6
#define dirTras   5
#define esqFrente 10
#define esqTras   11

// DEFINIÇÕES
#define LINHA HIGH

#define FRENTE  1
#define PARADO  0
#define TRAS   -1

// DECLARAÇÃO DE FUNÇÕES
void configMotor();
void motorEsq(int direcao, byte velocidade = 127);
void motorDir(int direcao, byte velocidade = 127);

// DECLARAÇÃO DE VARIÁVEIS
bool leituraEsquerda;
bool leituraDireita;

void setup() {
  Serial.begin(9600);

  pinMode(pinSensorDir, INPUT);
  pinMode(pinSensorEsq, INPUT);

  configMotor();
}

void loop() {
  bool valE = digitalRead(pinSensorEsq);
  bool valD = digitalRead(pinSensorDir);

  if (valD == LINHA && valE == LINHA) {
    motorEsq(PARADO);
    motorDir(PARADO);
  } else if (valD == LINHA) {
    motorDir(PARADO);
    motorEsq(FRENTE, 80);
  } else if (valE == LINHA) {
    motorEsq(PARADO);
    motorDir(FRENTE, 800);
  } else {
    motorDir(FRENTE);
    motorEsq(FRENTE);
  }
}

// IMPLEMENTO DE FUNÇÕES

void configMotor() {
  pinMode(dirFrente,  OUTPUT);
  pinMode(dirTras,    OUTPUT);
  pinMode(esqFrente,  OUTPUT);
  pinMode(esqTras,    OUTPUT);

  digitalWrite(dirFrente,  LOW);
  digitalWrite(dirTras,    LOW);
  digitalWrite(esqFrente,  LOW);
  digitalWrite(esqTras,    LOW);
}

void motorEsq(int direcao, byte velocidade) {
  switch (direcao) {
    case -1: {
        //        Serial.println("Esq Trás");
        digitalWrite(esqFrente,  LOW);
        analogWrite (esqTras,    velocidade);
        break;
      }
    case 0: {
        //        Serial.println("Esq PARADOdo");
        digitalWrite(esqFrente,  HIGH);
        digitalWrite(esqTras,    HIGH);
        break;
      }
    case 1: {
        //        Serial.println("Esq Frente");
        analogWrite (esqFrente,  velocidade);
        digitalWrite(esqTras,    LOW);
        break;
      }
  }
}

void motorDir(int direcao, byte velocidade                                                                                                                                               ) {
  switch (direcao) {
    case -1: {
        //        Serial.println("Dir Trás");
        digitalWrite(dirFrente,  LOW);
        analogWrite (dirTras,    velocidade);
        break;
      }
    case 0: {
        //        Serial.println("Dir PARADOdo");
        digitalWrite(dirFrente,  HIGH);
        digitalWrite(dirTras,    HIGH);
        break;
      }
    case 1: {
        //        Serial.println("Dir Frente");
        analogWrite (dirFrente,  velocidade);
        digitalWrite(dirTras,    LOW);
        break;
      }
  }
}
